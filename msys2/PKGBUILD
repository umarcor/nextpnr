_realname=nextpnr
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=ci
pkgrel=1
pkgdesc="Portable FPGA place and route tool (mingw-w64)"
arch=('any')
url="https://github.com/YosysHQ/nextpnr"
license=('ISC')
groups=("${MINGW_PACKAGE_PREFIX}-eda")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-qt5"
         "${MINGW_PACKAGE_PREFIX}-python")
makedepends=("${MINGW_PACKAGE_PREFIX}-boost"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-eigen3"
             "${MINGW_PACKAGE_PREFIX}-icestorm"
             "${MINGW_PACKAGE_PREFIX}-openmp"
             "${MINGW_PACKAGE_PREFIX}-python"
             "${MINGW_PACKAGE_PREFIX}-qt5")

source=()
sha256sums=()

build() {
  cd "${srcdir}"/../..

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" cmake \
    -G "MSYS Makefiles" \
    -DICESTORM_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_PREFIX_PATH="${MINGW_PREFIX}" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DARCH=ice40 \
    -DBUILD_HEAP=ON \
    -DUSE_OPENMP=ON \
    -DBUILD_PYTHON=ON \
    -DBUILD_GUI=ON \
    -DCMAKE_VERBOSE_MAKEFILE:BOOL=ON \
    -DBUILD_TESTS=ON \
    .

  cmake --build . --config Release
}

check() {
  cd "${srcdir}"/../..
  make test
}

package() {
  cd "${srcdir}"/../..

  make DESTDIR="${pkgdir}" install

  _licenses="${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"
  mkdir -p "${_licenses}"
  install -m 644 "${srcdir}"/../../COPYING "${_licenses}"
}
